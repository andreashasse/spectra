-module(example_test).

-include_lib("eunit/include/eunit.hrl").

-include("../include/spectra.hrl").

-ifdef(OTP_RELEASE).

-if(?OTP_RELEASE >= 27).

%% Using json.erl in this module.

-record(email_contact, {address, verified, domain}).
-record(phone_contact, {number, verified, sms_capable}).

-type verified() ::
    #{source := one_time_code | gut_feeling, string() => string()} | undefined.
-type email_contact() ::
    #email_contact{
        address :: string(),
        verified :: verified(),
        domain :: string()
    }.
-type phone_contact() ::
    #phone_contact{
        number :: string(),
        verified :: verified(),
        sms_capable :: boolean()
    }.
-type contacts() :: [email_contact() | phone_contact()].

example_json_roundtrip_test() ->
    Contacts =
        [
            #email_contact{
                address = "john.doe@example.com",
                verified = #{source => one_time_code, "code" => "123456"},
                domain = "example.com"
            },
            #phone_contact{
                number = "+1-555-123-4567",
                verified = #{source => gut_feeling, "confidence" => "high"},
                sms_capable = true
            },
            #email_contact{address = "alice@company.org", domain = "company.org"}
        ],

    Json = contacts_to_json(Contacts),
    %% io:format("JSON Output: ~p~n", [Json]),
    ?assertMatch({ok, Contacts}, json_to_contacts(Json)).

bad_source_json_test() ->
    BadSourceJson =
        <<
            "[{\"number\":\"+1-555-123-4567\",\n"
            "             \"verified\":{\"source\":\"a_bad_source\"},\n"
            "             \"sms_capable\":true}]"
        >>,
    ?assertMatch({error, [#sp_error{}]}, json_to_contacts(BadSourceJson)).

-spec json_to_contacts(binary()) -> {ok, contacts()} | {error, [spectra:error()]}.
json_to_contacts(Json) ->
    Decoded = json:decode(Json),
    spectra:decode(json, ?MODULE, {type, contacts, 0}, Decoded, [pre_decoded]).

-spec contacts_to_json(contacts()) -> binary() | {error, [spectra:error()]}.
contacts_to_json(Contacts) ->
    maybe
        {ok, Encodeable} ?=
            spectra:encode(json, ?MODULE, {type, contacts, 0}, Contacts, [pre_encoded]),
        iolist_to_binary(json:encode(Encodeable))
    end.

-endif.
-endif.
